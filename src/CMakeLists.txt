# lbc library
add_library(lbc_lib)

# lbc sources
target_sources(lbc_lib
    PRIVATE
    Ast/Ast.cpp
    Ast/AstExprPrinter.cpp
    Driver/Context.cpp
    Lexer/Lexer.cpp
    Lexer/Token.cpp
    Parser/ParseDecl.cpp
    Parser/ParseExpr.cpp
    Parser/ParseStmt.cpp
    Parser/ParseType.cpp
    Parser/Parser.cpp
    Type/Type.cpp

    PUBLIC FILE_SET HEADERS
    BASE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}
    FILES
    Ast/AstExprPrinter.hpp
    Driver/Context.hpp
    Lexer/Character.hpp
    Lexer/Cursor.hpp
    Lexer/Lexer.hpp
    Lexer/Token.hpp
    Parser/Parser.hpp
    Type/Type.hpp
    Utilities/Diagnostics.hpp
    Utilities/LiteralValue.hpp
    Utilities/NoCopy.hpp
    Utilities/Try.hpp
    Utilities/ValueRestorer.hpp
    Utilities/Visitor.hpp
)

# Sources generated by tbl-gen tools
add_tblgen(lbc_lib lbc-tblgen
    GENERATOR lbc-tokens-def INPUT Lexer/TokenKind.td
    GENERATOR lbc-ast-def INPUT Ast/Ast.td
    GENERATOR lbc-ast-visitor INPUT Ast/Ast.td OUTPUT AstVisitor.hpp
)

# target settings
target_link_libraries(lbc_lib PRIVATE compiler_options compiler_warnings)
target_include_directories(lbc_lib PRIVATE "${CMAKE_BINARY_DIR}/configured_files/include")

# precompile header
target_precompile_headers(lbc_lib PUBLIC pch.hpp)

# link llvm
configure_llvm(lbc_lib)

# lbc executable
add_executable(lbc main.cpp)
target_link_libraries(lbc PRIVATE lbc_lib compiler_options compiler_warnings)
target_precompile_headers(lbc REUSE_FROM lbc_lib)
set_target_properties(lbc PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${PROJECT_SOURCE_DIR}/bin)
