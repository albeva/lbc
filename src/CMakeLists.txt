# lbc library
add_library(lbc_lib
    Lexer/Character.hpp)

# lbc sources
target_sources(lbc_lib
    PRIVATE
    Driver/Context.cpp
    Lexer/Lexer.cpp
    Lexer/Token.cpp
    Parser/Parser.cpp

    PUBLIC FILE_SET HEADERS
    BASE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}
    FILES
    Driver/Context.hpp
    Lexer/Character.hpp
    Lexer/Cursor.hpp
    Lexer/Lexer.hpp
    Lexer/Token.hpp
    Parser/Parser.hpp
    Utilities/Diagnostics.hpp
    Utilities/LiteralValue.hpp
    Utilities/NoCopy.hpp
    Utilities/Try.hpp
)

# Sources generated by tbl-gen tools
add_tblgen(lbc_lib lbc-tblgen
    GENERATOR lbc-tokens-def INPUT Lexer/Tokens.td
)

# target settings
target_link_libraries(lbc_lib PRIVATE compiler_options compiler_warnings)
target_include_directories(lbc_lib PRIVATE "${CMAKE_BINARY_DIR}/configured_files/include")

# precompile header
target_precompile_headers(lbc_lib PUBLIC pch.hpp)

# link llvm
configure_llvm(lbc_lib)

# lbc executable
add_executable(lbc main.cpp)
target_link_libraries(lbc PRIVATE lbc_lib compiler_options compiler_warnings)
target_precompile_headers(lbc REUSE_FROM lbc_lib)
set_target_properties(lbc PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${PROJECT_SOURCE_DIR}/bin)
